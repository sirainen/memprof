dnl Process this file with autoconf to produce a configure script.

AC_INIT(memprof.glade)

dnl $Format: "AM_INIT_AUTOMAKE(MemProf, $ReleaseVersion$)" $
AM_INIT_AUTOMAKE(memprof, 0.3.0)

dnl Specify a configuration file
AM_CONFIG_HEADER(config.h)

dnl Initialize libtool
AM_PROG_LIBTOOL

dnl Initialize maintainer mode
AM_MAINTAINER_MODE

AC_ISC_POSIX

AC_PROG_CC
AM_PROG_CC_STDC
AC_PROG_INSTALL

changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

dnl Initialize gettext
ALL_LINGUAS="da et fr it ja no ru sv tr"
AM_GNU_GETTEXT

dnl GNOME checks (not using macros/ directory)

AC_PATH_PROG(GNOME_CONFIG, gnome-config, no)
if test "x$GNOME_CONFIG" = "xno" ; then
  AC_MSG_ERROR([*** GNOME must be installed to compile memprof])
fi

GNOME_DATADIR=`gnome-config --datadir`
AC_SUBST(GNOME_DATADIR)

MEMPROF_CFLAGS=`gnome-config --cflags gnomeui libglade`
AC_SUBST(MEMPROF_CFLAGS)

MEMPROF_LIBS=`gnome-config --libs gnomeui libglade`

AC_CHECK_LIB(glade-gnome, glade_gnome_init,:,
    AC_MSG_ERROR([*** libglade and libglade-gnome are required to compile MemProf]), $MEMPROF_LIBS)


AC_CHECK_LIB(iberty, cplus_demangle,:, 
  AC_CHECK_LIB(iberty, cplus_demangle_opname, LIBS="$LIBS -ldl",
    AC_MSG_ERROR([*** libiberty required to compile MemProf]), -ldl))

AC_CHECK_LIB(bfd, bfd_get_error, [MEMPROF_LIBS="$MEMPROF_LIBS -Wl,-Bstatic -lbfd -liberty -Wl,-Bdynamic"], 
  AC_MSG_ERROR([*** libbfd required to compile MemProf]),
  -liberty)

if ! uname | grep Linux > /dev/null ; then
  AC_MSG_ERROR([*** MemProf only works on linux])
fi

AC_SUBST(MEMPROF_LIBS)

dnl Output the Makefiles

AC_OUTPUT([
Makefile
intl/Makefile
po/Makefile.in
memprof.spec
])
