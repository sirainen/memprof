dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.52)

AC_INIT(memprof, 0.5.1)
AC_CONFIG_SRCDIR(memprof.glade)

dnl $Format: "AM_INIT_AUTOMAKE(MemProf, $ReleaseVersion$)" $
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)

dnl Specify a configuration file
AM_CONFIG_HEADER(config.h)

dnl Initialize libtool
AM_PROG_LIBTOOL

dnl Initialize maintainer mode
AM_MAINTAINER_MODE

AC_PROG_CC
AM_PROG_CC_STDC
AC_PROG_INSTALL

AC_PROG_INTLTOOL

changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

dnl GNOME checks (using pkg-config macros) and getting cflags and libs
GTK_REQUIRED=2.0.0
GCONF_REQUIRED=1.1.8
LIBGNOMEUI_REQUIRED=1.112.1
LIBGLADE_REQUIRED=1.99.9

PKG_CHECK_MODULES(MEMPROF,
[
	gtk+-2.0 >= $GTK_REQUIRED
	gconf-2.0 >= $GCONF_REQUIRED
        libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED
        libglade-2.0 >= $LIBGLADE_REQUIRED
])

AC_SUBST(MEMPROF_CFLAGS)

AC_CHECK_LIB(iberty, cplus_demangle,:, 
  AC_CHECK_LIB(iberty, cplus_demangle_opname, LIBS="$LIBS -ldl",
    AC_MSG_ERROR([*** libiberty required to compile MemProf]), -ldl))

AC_CHECK_LIB(bfd, bfd_get_error, [MEMPROF_LIBS="$MEMPROF_LIBS -Wl,-Bstatic -lbfd -liberty -Wl,-Bdynamic"], 
  AC_MSG_ERROR([*** libbfd required to compile MemProf]),
  -liberty)

if ! uname | grep Linux > /dev/null ; then
  AC_MSG_ERROR([*** MemProf only works on linux])
fi

AC_SUBST(MEMPROF_LIBS)

AC_PATH_PROG(GCONFTOOL, gconftool-2, no)

if test x"$GCONFTOOL" = xno; then
  AC_MSG_ERROR([gconftool-2 executable not found in your path - should be installed with GConf])
fi

AM_GCONF_SOURCE_2

dnl Initialize gettext
GETTEXT_PACKAGE=memprof
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE")

ALL_LINGUAS="az ca da de el es et fr hu it ja ko ms nl no pl pt pt_BR ru sk sl sv tr uk vi zh_CN"
AM_GLIB_GNU_GETTEXT

AC_HAVE_HEADERS(execinfo.h)
AC_CHECK_FUNCS(bind_textdomain_codeset backtrace)


dnl Remember ACLOCAL_FLAGS for automatic reautogen.
ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

dnl Output the Makefiles

AC_CONFIG_FILES([
Makefile
po/Makefile.in
memprof.spec
])
AC_OUTPUT
